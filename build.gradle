plugins {
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'org.hl7.fhir'
version = '0.0.1'
def applicationName = 'fhir.ig'
def xtendGenDir = file('src/main/xtend-gen')

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'src/main/java'
            srcDirs += 'src/main/resources'
            srcDirs += xtendGenDir   
        }
    }
}

configurations {
    xtendCompiler
}

configurations.all {
    resolutionStrategy {
        force 'org.slf4j:slf4j-api:2.0.0'
    }
}

dependencies {
    // Core FHIR/EMF
    implementation libs.fhirEmf
    implementation libs.emfCommon
    implementation libs.emfEcore
    implementation libs.emfEcoreXMI
    implementation libs.jacksonAnnotations
    implementation libs.jacksonDatabind
    implementation libs.jacksonXml

    // Spring Boot Web stack
    implementation libs.springWeb
    implementation libs.springActuator
    implementation libs.context
    implementation libs.springTest
    implementation libs.springSessionDataRedis
    implementation libs.redis

    // Xtend compiler + runtime
    xtendCompiler libs.xtextXbase
    xtendCompiler libs.xtendCore
    xtendCompiler libs.xtendLib

    implementation libs.xtextXbase
    implementation libs.xtendLib

    // Logging
    implementation libs.slf4jAPI
    implementation libs.logbackClassic

    // Testing
    testImplementation libs.logbackClassic
    testImplementation libs.junitJupiter
    testImplementation libs.junitJupiterAPI
    testImplementation libs.emfEcoreXMI
}

tasks.named('clean') {

    if (xtendGenDir.exists()) {
        println "Deleting $xtendGenDir"
        delete xtendGenDir
    }

}

task compileXtend(type: JavaExec) {
    group = "build"
    description = "Compiles Xtend sources to Java"
    classpath = configurations.xtendCompiler
    mainClass = "org.eclipse.xtend.core.compiler.batch.Main"

    doFirst {
        // Ensure directory exists
        xtendGenDir.mkdirs()
    }

    args = [
        "src/main/xtend",              // Xtend source folder
        "-d", xtendGenDir,             // Output dir for generated Java
        "-classpath", configurations.xtendCompiler.asPath + File.pathSeparator + sourceSets.main.runtimeClasspath.asPath
    ]
}
compileJava.dependsOn compileXtend

bootJar {
    mainClass = 'org.hl7.fhir.FhirIgApplication'
}

// jar {
//     manifest {
//         attributes 'Main-Class': provider { application.mainClass.get() }
//     }
//     duplicatesStrategy = DuplicatesStrategy.EXCLUDE
//     from {
//         configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//     }
//     exclude("META-INF/*.RSA", "META-INF/*.DSA", "META-INF/*.SF", "META-INF/MANIFEST.MF")
// }

processResources {
    from('src/main/java') {
        include '**/*.ecore'
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

task checkSignedJars {
    doLast {
        println "Scanning for signed JARs..."
        configurations.runtimeClasspath.files.each { jarFile ->
            if (jarFile.name.endsWith(".jar")) {
                println "Checking: ${jarFile.name}"
                def hasSignature = false
                zipTree(jarFile).visit { file ->
                    if (file.name.matches("META-INF/.*\\.(SF|RSA|DSA|EC)")) {
                        println "Found signed JAR: ${jarFile.name} (Signature File: ${file.name})"
                        hasSignature = true
                    }
                }
                if (!hasSignature) {
                    println "No signature found in: ${jarFile.name}"
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = applicationName
            version = project.version
            from(components.java)
        }
    }
}
